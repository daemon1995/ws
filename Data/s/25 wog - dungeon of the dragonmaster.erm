ZVSE

** Dungeon of the Dragonmaster
** Wogify Name: script53.erm
** This strengthens all Dragon Utopias an a map. AI will visit normal DU but gets some extra bonuses.

** Замена WoG-скрипта "Темница хозяина драконов"
** - Охрана Утопий и награда в золоте на 25..100% больше обычных
** - После взятия не появляется специальных заграждений
** - Золото Утопии не может быть обменяно на опыт
** - Утопии могут быть 4х типов:
** 1. Обычная Утопия   (+25% сила охраны и золото)  - 3x Черные,    3x Золотые,  6x Красные,       6x Зеленые драконы.
** 2. Элитная Утопия   (+50% сила охраны и золото)  - 2x Темные,    2x Алмазные, 4x Черные,        4x Золотые драконы.
** 3. Неживая Утопия   (+75% сила охраны и золото)  - 2x Драколичи, 4x Кровавые, 6x Призрачные,    8x Костяные драконы.
** 4. Эпическая Утопия (+100% сила охраны и золото) - 1x Лазурные,  2x Кристаллические, 3x Ржавые, 4x Волшебные драконы.

!?FU(OnAfterErmInstructions);           // Перед показом карты
!!UN:P53/?y1;    !!FU&y1=0:E;           // выход, если опция не включена
!!UN:U25/-1/?y1; !!VRv1:S-1;            // y1 - количество употий
!!DO(wog53.ChangeDU)/1/y1/1:P;          // модификация Утопий

!?FU(wog53.ChangeDU);                   // модификация Утопий
!!UN:U25/-1/-1/1;                       // v1-v3 - координаты Утопии
!!CBv1/v2/v3:R6/?y4;                    // y4 - Количество золота в утопии (20/30/60/50к)

; 3x Черные, 3x Золотые, 6x Красные, 6x Зеленые драконы, Награда 30к золота
!!CBv1/v2/v3&y4=20000:G0/83/3  G1/27/3  G2/82/6  G3/26/6  G4/-1/0 G5/-1/0 G6/-1/0 R6/30000;

; 2x Темные, 2x Алмазные, 4x Черные, 4x Золотые драконы, Награда 50к золота
!!CBv1/v2/v3&y4=30000:G0/155/2 G1/151/2 G2/83/4  G3/27/4  G4/-1/0 G5/-1/0 G6/-1/0 R6/50000;

; 2x Драколичи, 4x Кровавые, 6x Призрачные, 8x Костяные драконы, Награда 70к золота
!!CBv1/v2/v3&y4=40000:G0/196/2 G1/154/4 G2/69/6  G3/68/8  G4/-1/0 G5/-1/0 G6/-1/0 R6/70000;

; 1x Лазурные, 2x Кристаллические, 3x Ржавые, 4x Волшебные драконы, Награда 100к золота
!!CBv1/v2/v3&y4=50000:G0/132/1 G1/133/2 G2/135/3 G3/134/4 G4/-1/0 G5/-1/0 G6/-1/0 R6/100000;
!!UN:P67/?y5;  // если включен скрипт "Нейтральный город"
!!CBv1/v2/v3&y4=50000/y5=1:G2/135/d3;

** end

ZVSE

** ENHANCED COMMANDERS  -  ERM script 51 (ERA II: 27-wog)

** By Arstahd
** Version: 1.11        updated October 2 2004
** Updated September 15, 2004 to change local y variables in instructions to v variables
** Special Thanks to Hermann the Weird for help with bug fixing and script maintenance

** Note: Commander Enhancements are disabled for network human-to-human battles.

** Variables used:
** V variables:    v7080-v7092, v7185
** Timers used:  none

*** commander stats are calculated to new formulae
**    display will be inaccurate when hero levels up, re-open commander screen to update stat display, ignore skill descriptions
**    attack          - base 5 + 1/2 x level - skill adds +25%,+50%,+75%,+100%,+125%
**    defence         - base 5 + 1/2 x level - skill adds +25%,+50%,+75%,+100%,+125%
**    hit points      - base 30 + 15 x level - skill adds +25%,+50%,+75%,+100%,+125%
**    damage          - base 10 + 5 x level  - skill adds +25%,+50%,+75%,+100%,+125%
**    magic power     - base 3 + 3 x skill level  (Soul Eater & Astral Spirit: base 1 + 1 x skill level)
**    magic resistance- base 0 + 10 x skill level
**    speed           - base 5 - skill adds normal bonus
**    spell quantity  - functions as normal (increased by magic power skill)
*** commanders each start with a special ability
**    paladin         - endless retaliation
**    heirophant      - fire shield
**    temple guardian - shoot
**    succubus        - fly
**    soul eater      - fear
**    brute           - no retaliation
**    ogre leader     - block
**    shaman          - death stare
**    astral spirit   - ignore 50% defense
*** commander spells improved, most apply an additional spell (at adv. level) after they cast a spell
**    paladin         - cure/bless
**    heirophant      - shield/mirth
**    temple guardian - precision/prayer
**    succubus        - fire shield/bloodlust  (bloodlust displays Sacrifice animation)
**    soul eater      - animate dead           (no change)
**    brute           - bloodlust/stoneskin
**    ogre leader     - stoneskin/counterstrike
**    shaman          - haste/fortune
**    astral spirit   - summon elemental       (counterstrike removed)
*** commanders (except soul eater &astral spirit) are now living and can be resurrected (soul eater can still be raised with animate dead)
*** commanders are town based if bought, type depends on town not hero when you buy a new commander (dismiss old commander to buy a new one)

*** commander stones now give skills:
**    opal - gives random special ability
**    ruby - gives either +1 to Attack skill or Damage skill
**    sapphire - gives either +1 to Defense skill or Hit Points skill
**    emerald - gives either +1 to Magic skill or Speed skill

** initialization
!?PI;
!!UN:P51/?y1;                                    [check if Enhanced Commanders is enabled in WoGify Options]
!!FU&y1<>1:E;                                    [exit if not enabled]
!!VRv7185:S1;                                    [set var v7185 is true]
!!UN:P3/?v1;                                     [check if Commanders are enabled]
!!VRv7185&v1=1:S0;                               [disable if Commanders aren't enabled]
!!CO-2&v7185=1:P1;                               [enable custom primary skill control]
!!UN:B6/1;                                       [set new chest 6 to advanced setting (disabled)]

** function to make commanders alive
!!DO(WOG27_SetNPC_Alive)/174/191/1&v7185=1:P;    [iterate through all commander types and make alive]

!?FU(WOG27_SetNPC_Alive);
!!MA:Xx16/?y1;                                   [get flags]
!!VRy1:|16;                                      [add alive bit]
!!VRy1|x16=178/x16=187/x16=182:-16;
!!MA:Xx16/y1;                                    [reset flags]


** commander screen open trigger
!?CO0&v7185=1;                                   [check if Enhanced Commanders is enabled]
!!HE-1:N?y1;                                     [get hero's (commander's) number]
!!FU(WOG27_NPC_EditStats):Py1;
** end of hero level trigger

** start of pre-battle trigger **
!?BA0&v7185=1/-998;                              [check if Enhanced Commanders is enabled]
!!VRv7080:C-1/-1/-1/-1/-1/-1/-1/-1/-1/-1;        [initialize variables]
!!BA:H0/?y1 H1/?y2;                              [get heroes (commanders) number]

!!FU(WOG27_NPC_EditStats)&y1>-1:Py1;             [modify commander stats - attacker]
!!FU(WOG27_NPC_EditStats)&y2>-1:Py2;             [modify commander stats - defender]

!!COy1&y1>-1:B0/?y3 T?v7088;                     [check skill bits, get type - attacker]
!!COy2&y2>-1:B0/?y4 T?v7089;                     [check skill bits, get type - defender]
** end of pre-battle trigger


** pre-action trigger **
!?BG&v7185=1/-998;                               [continue if enabled]
!!BG:N?v7080 A?v7081 E?v7082;                    [get current stack, action type, destination stack]
!!BMv7082&v7082>-1:R?v7087;                      [get retaliations of target creature stack]
!!BMv7080&v7080>-1:T?v7092;                      [get type of current creature stack]


!!VRy-2&v7092=182/v7081=10:S66 T3;               [if astral spirit is casting - choose random elemental spell]
!!BMv7080&v7092=182/v7081=10:U4/y-2;             [if astral spirit is casting - change spell to summon elemental]
!!VRy-2&v7092=191/v7081=10:S66 T3;               [if astral spirit is casting - choose random elemental spell]
!!BMv7080&v7092=191/v7081=10:U4/y-2;             [if astral spirit is casting - change spell to summon elemental]

** end of pre-action trigger **

** post-action trigger **
!?BG1&v7185=1/-998;                              [continue if enabled]
!!BMv7080&v7080>-1:T?y-3 I?y-4;                  [get acting monster type and side]
!!FU(WOG27_NPC_CastSpell)&y-3>=174/y-3<=191/v7081=10:Pv7080/y-3;  [cast extra spell if commander cast]

!!BMv7082&v7082>-1:F?y-7 P?y-8;                  [get target's flags, position]

!!BMv7080&v7092=182/v7081=10:U4/58;              [astral spirit - revert spell to counterstrike]
!!BMv7080&v7092=191/v7081=10:U4/58;              [astral spirit - revert spell to counterstrike]

**  retaliation attack
!!BMv7082&v7082>-1:T?y-3 I?y-4 P?y-5;            [get target monster type, side, position]
!!BU&v7082>-1:Ey-5/?y-6;                         [check if target is still alive]

!!BMv7080&v7080>-1:F?y-9;                        [get flags of attacking creature stack]
!!VRy-9&v7080>-1:&65536;                         [check for no retaliation]
** end of post-action trigger **


** post-buy commander trigger **
!?CO2&v7185=1;                                   [continue if enabled and "choose commanders" isn't]
!!UN:P186/?y1;                                   [check if choose Commanders is enabled in WoGify Options]
!!FU&y1=1:E;                                     [exit if choose Commanders script is enabled]
!!HE-1:P?y1/?y2/?y3;                             [get hero position]
!!OBy1/y2/y3:U?y4;                               [get subtype of town]
!!CO-1:Ty4;                                      [set commander type]
** end of post-buy commander trigger **

** function to edit commander stats
!?FU(WOG27_NPC_EditStats);
* x1 - hero number
!!COx1:X2/?y2 T?y5;                              [get commander level & type]
** attack - base 5 + 1/2 x level - skill adds +25%,+50%,+75%,+100%,+125%
!!COx1:S0/?y3;                                   [get attack level]
!!VRy4&y3=0:Sy2 :2 +5;                           [if attack=0 set attack to 5 + 1/2 level]
!!VRy4&y3=1:Sy2 :2 +5 *125 :100 -2;              [if attack=1 set attack to 5 + 1/2 level x 125%]
!!VRy4&y3=2:Sy2 :2 +5 *150 :100 -5;              [if attack=2 set attack to 5 + 1/2 level x 150%]
!!VRy4&y3=3:Sy2 :2 +5 *175 :100 -9;              [if attack=3 set attack to 5 + 1/2 level x 175%]
!!VRy4&y3=4:Sy2 :2 +5 *2 -15;                    [if attack=4 set attack to 5 + 1/2 level x 200%]
!!VRy4&y3=5:Sy2 :2 +5 *225 :100 -25;             [if attack=5 set attack to 5 + 1/2 level x 225%]
!!COx1:P0/y4;                                    [set attack]
** defense - base 5 + 1/2 x level - skill adds +25%,+50%,+75%,+100%,+125%
!!COx1:S1/?y3;                                   [get defence level]
!!VRy4&y3=0:Sy2 :2 +5;                           [if defence=0 set defence to 5 + 1/2 level]
!!VRy4&y3=1:Sy2 :2 +5 *125 :100 -4;              [if defence=1 set defence to 5 + 1/2 level x 125%]
!!VRy4&y3=2:Sy2 :2 +5 *150 :100 -10;             [if defence=2 set defence to 5 + 1/2 level x 150%]
!!VRy4&y3=3:Sy2 :2 +5 *175 :100 -18;             [if defence=3 set defence to 5 + 1/2 level x 175%]
!!VRy4&y3=4:Sy2 :2 +5 *2 -30;                    [if defence=4 set defence to 5 + 1/2 level x 200%]
!!VRy4&y3=5:Sy2 :2 +5 *225 :100 -50;             [if defence=5 set defence to 5 + 1/2 level x 225%]
!!COx1:P1/y4;                                    [set defence]
** hit points - base 30 + 15 x level - skill adds +25%,+50%,+75%,+100%,+125%
!!COx1:S2/?y3;                                   [get hit points level]
!!VRy4&y3=0:Sy2 *20 +40;                         [if hit points=0 set hit points to 40 + 20 x level]
!!VRy4&y3=1:Sy2 *20 +40 *125 :110;               [if hit points=1 set hit points to 40 + 20 x level x 125%]
!!VRy4&y3=2:Sy2 *20 +40 *150 :125;               [if hit points=2 set hit points to 40 + 20 x level x 150%]
!!VRy4&y3=3:Sy2 *20 +40 *175 :145;               [if hit points=3 set hit points to 40 + 20 x level x 175%]
!!VRy4&y3=4:Sy2 *20 +40 *200 :170;               [if hit points=4 set hit points to 40 + 20 x level x 200%]
!!VRy4&y3=5:Sy2 *20 +40 *225 :200;               [if hit points=5 set hit points to 40 + 20 x level x 225%]
!!COx1:P2/y4;                                    [set hit points]
** damage - base 10 + 5 x level - skill adds +25%,+50%,+75%,+100%,+125%
!!COx1:S3/?y3;                                   [get damage level]
!!VRy4&y3=0:Sy2 *5 +10;                          [if damage=0 set damage to 10 + 5 x level]
!!VRy4&y3=1:Sy2 *5 +10 *125 :110;                [if damage=1 set damage to 10 + 5 x level x 125%]
!!VRy4&y3=2:Sy2 *5 +10 *150 :125;                [if damage=2 set damage to 10 + 5 x level x 150%]
!!VRy4&y3=3:Sy2 *5 +10 *175 :145;                [if damage=3 set damage to 10 + 5 x level x 175%]
!!VRy4&y3=4:Sy2 *5 +10 *200 :170;                [if damage=4 set damage to 10 + 5 x level x 200%]
!!VRy4&y3=5:Sy2 *5 +10 *225 :200;                [if damage=5 set damage to 10 + 5 x level x 225%]
!!COx1:P3/y4;                                    [set damage]
** magic power - base 3 + 3 x skill level   (Soul Eater: base 1 + 1 x skill level)
!!COx1:S4/?y3;                                   [get MP level]
!!VRy6:Sy3 +1 *3;                                [set power to (MP+1)x3]
!!VRy6&y5=4:Sy3 +1;                              [if Soul Eater set power MP+1]
!!VRy6&y5=8:Sy3 +1;                              [if Astral Spirit set power MP+1]
!!VRy4&y3=0:Sy6;                                 [if MP=0 set power]
!!VRy4&y3=1:Sy6 -1;                              [if MP=1 set power]
!!VRy4&y3=2:Sy6 -3;                              [if MP=2 set power]
!!VRy4&y3=3:Sy6 -7;                              [if MP=3 set power]
!!VRy4&y3=4:Sy6 -14;                             [if MP=4 set power]
!!VRy4&y3=5:Sy6 -29;                             [if MP=5 set power]
!!COx1:P4/y4;                                    [set power]
** speed - base 5 - skill adds normal bonus
!!COx1:P5/5;                                     [set base speed to 5]

** starting special bonus
!!COx1&y5=0:B1/5/1;                              [give paladin         - endless retaliation]
!!COx1&y5=1:B1/7/1;                              [give hierophant      - fire shield]
!!COx1&y5=2:B1/4/1;                              [give temple guardian - shoot]
!!COx1&y5=3:B1/14/1;                             [give succubus        - fly]
!!COx1&y5=4:B1/1/1;                              [give soul eater      - fear]
!!COx1&y5=5:B1/3/1;                              [give brute           - no retaliation]
!!COx1&y5=6:B1/8/1;                              [give ogre leader     - block]
!!COx1&y5=7:B1/12/1;                             [give shaman          - death stare]
!!COx1&y5=8:B1/0/1;                              [give astral spirit   - ignore 50% defense]

** end of function


** function to cast spell    x1=acting stack x2=type x3=rank x4=power x5=no retaliation flag
* x1=spell x2=position x3=rank x4=power
!?FU(WOG27_NPC_CastSpell);
!!BMv7082&v7082>-1:P?y8;                         [get position of target creature stack]
!!HE-1:N?y5;                                     [get hero's (commander's) number]
!!COy5:P4/?y6;                                   [get power]

!!BMv7082|x2=174/x2=183:V36 M41/y6/2;            [apply bless - paladin]
!!BMv7082|x2=175/x2=184:V20 M49/y6/2;            [apply mirth - heirophant]
!!BMv7082|x2=176/x2=185:V0 M48/y6/2;             [apply prayer - temple guardian]
!!BMv7082|x2=177/x2=186:V51 M43/y6/2;            [apply bloodlust - succubus]
!!BMv7082|x2=179/x2=188:V54 M46/y6/2;            [apply stoneskin - brute]
!!BMv7082|x2=180/x2=189:V7 M58/y6/2;             [apply counterstrike - ogre leader]
!!BMv7082|x2=181/x2=190:V18 M51/y6/2;            [apply fortune - shaman]
** end of function **


**  object trigger - opal
!?OB101/7&v7185=1;
!!HE-1:N?y1;
!!VRy4:S0 T14;                                   [random roll 0-14]
!!VRy2:S151000 +y4;                              [apply string]
!!VRz2:Szy2;                                     [set string]

!!COy1:B1/y4/?y5 D?y6 E?y7 N?z1;                 [check if commander has skill, is alive, is enabled, get name]
!!OB998&y6=0/y7=1:T101 U6;                       [set to chest 6 if commander is alive, enabled]
!!COy1&y5=0/y6=0/y7=1:B1/y4/1;                   [give skill if commander doesn't have it and is alive, enabled]

!!IF&y5=0/y6=0/y7=1/1000:M1/z151015;
!!IF&y5=1/1000:M1/z151016;
** end of object trigger


**  object trigger - ruby
!?OB101/8&v7185=1;
!!FU(WOG27_NPC_AddSSkill):P0/3/151017/151018/151019/151020/151021;

**  object trigger - sapphire
!?OB101/9&v7185=1;
!!FU(WOG27_NPC_AddSSkill):P1/2/151022/151023/151024/151025/151026;

**  object trigger - emerald
!?OB101/10&v7185=1;
!!FU(WOG27_NPC_AddSSkill):P4/5/151027/151028/151029/151030/151031;

!?FU(WOG27_NPC_AddSSkill)&v7185=1;
* refactoring by [igrik] 15/05/2020
* x1 - first  sskill
* x2 - second sskill
* x3 - ert string id
!!HE-1:N?y1;
!!COy1:Sx1/?y2 Sx2/?y3 D?y6 E?y7 N?z1;
!!OB998&y6=0/y7=1:T101 U6;
!!COy1&y6=0/y7=1/y2>y3:Sx2/d1;
!!COy1&y6=0/y7=1/y2<y3:Sx1/d1;
!!VRy4&y6=0/y7=1/y2=y3/y2<5:S0 T99;
!!COy1&y6=0/y7=1/y2=y3/y2<5/y4<50:Sx2/d1;
!!COy1&y6=0/y7=1/y2=y3/y2<5/y4>49:Sx1/d1;

!!IF&1000/y6=0/y2>y3:M1/zx3;
!!IF&1000/y6=0/y2<y3:M1/zx4;
!!IF&1000/y6=0/y2=y3/y2<5/y4<50:M1/zx5;
!!IF&1000/y6=0/y2=y3/y2<5/y4>49:M1/zx6;
!!IF&1000/y6=0/y2=y3/y2>4:M1/zx7;
** end of object trigger
